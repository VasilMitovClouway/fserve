apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
  mavenCentral()
}

configurations {
  providedCompile
}

javadoc {
  failOnError = false
  classpath = configurations.compile + configurations.providedCompile
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  sign configurations.archives
}

group = "com.clouway"
archivesBaseName = "fserve"
version = "1.0-SNAPSHOT"

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'fserve a friendly serving HTTP library'
        packaging 'jar'
        // optionally artifactId can be defined here
        description 'fserve a friendly serving HTTP library'
        url 'https://github.com/clouway/fserve'

        scm {
          connection 'scm:git:git@github.com:clouway/fserve.git'
          developerConnection 'scm:git:git@github.com:clouway/fserve.git'
          url 'https://github.com/clouway/fserve'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'clouway'
            name 'Miroslav Genov'
            email 'miroslav.genov@clouway.com'
          }
        }
      }
    }
  }
}

dependencies {
  compile 'com.google.guava:guava:18.0'
  compile 'com.google.code.gson:gson:2.6.2'

  providedCompile 'javax.servlet:servlet-api:2.5'

  testCompile 'junit:junit:4.11'
  testCompile 'org.hamcrest:hamcrest-all:1.3'
  testCompile 'org.jmock:jmock:2.6.0'
  testCompile 'org.jmock:jmock-junit4:2.6.0'
}


sourceSets.main.compileClasspath += configurations.providedCompile
sourceSets.test.compileClasspath += configurations.providedCompile
sourceSets.test.runtimeClasspath += configurations.providedCompile